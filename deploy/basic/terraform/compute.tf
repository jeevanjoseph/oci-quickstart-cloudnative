# Copyright (c) 2019, 2020 Oracle and/or its affiliates. All rights reserved.
# Licensed under the Universal Permissive License v 1.0 as shown at http://oss.oracle.com/licenses/upl.
# 

resource "oci_core_instance" "app_instance" {
  availability_domain                 = random_shuffle.compute_ad.result[count.index % length(random_shuffle.compute_ad.result)]
  compartment_id                      = var.compartment_ocid
  display_name                        = "mushop-${random_string.deploy_id.result}-${count.index}"
  shape                               = var.instance_shape
  is_pv_encryption_in_transit_enabled = var.is_pv_encryption_in_transit_enabled
  freeform_tags                       = local.common_tags

  create_vnic_details {
    subnet_id        = oci_core_subnet.mushop_main_subnet.id
    display_name     = "primaryvnic"
    assign_public_ip = (var.instance_visibility == "Private") ? false : true
    hostname_label   = "mushop-${random_string.deploy_id.result}-${count.index}"
  }

  source_details {
    source_type = "image"
    source_id   = lookup(data.oci_core_images.compute_images.images[0], "id")
    kms_key_id  = var.use_encryption_from_oci_vault ? (var.create_new_encryption_key ? oci_kms_key.mushop_key[0].id : var.encryption_key_id) : null
  }

  metadata = {
    ssh_authorized_keys        = var.generate_public_ssh_key ? tls_private_key.compute_ssh_key.public_key_openssh : var.public_ssh_key
    user_data                  = base64encode(data.template_file.mushop.rendered)
    db_name                    = oci_database_autonomous_database.mushop_autonomous_database.db_name
    atp_pw                     = random_string.autonomous_database_admin_password.result
    catalogue_sql_par          = "https://objectstorage.${var.region}.oraclecloud.com${oci_objectstorage_preauthrequest.catalogue_sql_script_preauth.access_uri}"
    apache_conf_par            = "https://objectstorage.${var.region}.oraclecloud.com${oci_objectstorage_preauthrequest.apache_conf_preauth.access_uri}"
    entrypoint_par             = "https://objectstorage.${var.region}.oraclecloud.com${oci_objectstorage_preauthrequest.entrypoint_preauth.access_uri}"
    mushop_app_par             = "https://objectstorage.${var.region}.oraclecloud.com${oci_objectstorage_preauthrequest.mushop_lite_preauth.access_uri}"
    wallet_par                 = "https://objectstorage.${var.region}.oraclecloud.com${oci_objectstorage_preauthrequest.mushop_wallet_preauth.access_uri}"
    assets_url                 = var.object_storage_mushop_media_visibility == "Private" ? "" : "https://objectstorage.${var.region}.oraclecloud.com/n/${oci_objectstorage_bucket.mushop_media.namespace}/b/${oci_objectstorage_bucket.mushop_media.name}/o/"
    oracle_client_version      = var.oracle_client_version
    mushop_media_visibility    = var.object_storage_mushop_media_visibility
    mushop_media_pars_list_par = "https://objectstorage.${var.region}.oraclecloud.com${oci_objectstorage_preauthrequest.mushop_media_pars_list_preauth.access_uri}"
  }

  count = var.num_nodes
}

### Important Security Notice ###
# The private key generated by this resource will be stored unencrypted in your Terraform state file. 
# Use of this resource for production deployments is not recommended. 
# Instead, generate a private key file outside of Terraform and distribute it securely to the system where Terraform will be run.

# Generate ssh keys to access Compute Nodes, if generate_public_ssh_key=true, applies to the Compute
resource "tls_private_key" "compute_ssh_key" {
  algorithm = "RSA"
  rsa_bits  = 2048
}